import java_cup.runtime.*;
import java.util.ArrayList;

action code {: 
                public void error(String s){
                    System.out.println(s);
                    System.exit(-1);
                }
            :}

parser code {: 
                TablaSimbolos ts;
            :}

/* Terminals (tokens returned by the scanner). */

terminal            ALL, CLL, AP, CP, AC, CC;
terminal            COMA, PYC;
terminal            ASIG;
terminal            MAS, MENOS, POR, DIV, MENOSUNARIO;
terminal            PRINT;
terminal            CONCATENA, INTERSECCION;
terminal            TRANSPUESTA, INVERSA, ADJUNTA;
terminal            SET;
terminal double[][] MATRIZ;
terminal double[]   VECTOR, CONJUNTO;
terminal String     IDENT;
terminal Double     NUMERO;


/* Non terminals */
non terminal                              axioma;
non terminal                              linea;
non terminal ArrayList<ArrayList<Double>> listaFilas, pseudoListaFilas;
non terminal ArrayList<Double>            listaNumeros;
non terminal double[][]                   matriz, pseudomatriz;
non terminal double[]                     vector, conjunto;
non terminal Double                       escalar;



/* Precedences */
precedence left     MAS, MENOS;
precedence left     POR, DIV;
precedence left     CONCATENA;
precedence left     INTERSECCION;
precedence nonassoc MENOSUNARIO;
precedence right     SET;

/* The grammar */

axioma          ::= axioma linea
                  | linea
                  ;

linea           ::= PRINT AP matriz:m CP PYC                {: Matrices.print(m); :}
                  | PRINT AP vector:v CP PYC                {: Matrices.print(v); :}
                  | PRINT AP conjunto:c CP PYC              {: Matrices.print(c); :}
                  | IDENT:s ASIG matriz:m PYC               {: 
                                                                if(TablaSimbolos.buscar(s)==null){
                                                                    TablaSimbolos.insertar(s,m);
                                                                } else {
                                                                    error(TablaSimbolos.ERROR_DUPLICADA);
                                                                }
                                                            :}
                  | IDENT:s ASIG vector:v PYC               {: 
                                                                if(TablaSimbolos.buscarVector(s)==null){
                                                                    TablaSimbolos.insertarVector(s,v);
                                                                } else {
                                                                    error(TablaSimbolos.ERROR_DUPLICADA);
                                                                }
                                                            :}
                  | IDENT:s AP escalar:n COMA escalar:m CP ASIG pseudomatriz:ma PYC    {:
                                                                double[][] res;
                                                                if(TablaSimbolos.buscar(s)!=null){
                                                                    error(TablaSimbolos.ERROR_DUPLICADA);
                                                                }
                                                                int p = Matrices.filas(ma);
                                                                int q = Matrices.columnas(ma);
                                                                if(n.intValue()>=p && m.intValue()>=q){
                                                                    res = Matrices.completar(ma, n.intValue(), m.intValue());
                                                                    TablaSimbolos.insertar(s, res);
                                                                } else if(n.intValue()>=p && m.intValue()<q){
                                                                    res = Matrices.completar(ma, n.intValue(), q);
                                                                    res = Matrices.subMatriz(res, n.intValue(), m.intValue());
                                                                    TablaSimbolos.insertar(s, res);
                                                                } else if(n.intValue()<p && m.intValue()>=q){
                                                                    res = Matrices.subMatriz(ma, n.intValue(), q);
                                                                    res = Matrices.completar(res, n.intValue(), m.intValue());
                                                                    TablaSimbolos.insertar(s, res);
                                                                } else if(n.intValue()<p && m.intValue()<q){
                                                                    res = Matrices.subMatriz(ma, n.intValue(), m.intValue());
                                                                    TablaSimbolos.insertar(s, res);
                                                                } else{
                                                                    System.out.println("ESTO NO DEBERIA OCURRIR");
                                                                }
                                
                                                                
                                                            :}
                  | IDENT:s ASIG conjunto:c PYC             {:
                                                                if(TablaSimbolos.check(s)){
                                                                    TablaSimbolos.insertarConjunto(s, c);
                                                                }
                                                            :}
                  ;

matriz          ::= ALL listaFilas:lf CLL                   {: RESULT = Matrices.toArray(lf); :}
                  | MATRIZ:s                                {: 
                                                                RESULT = s;
                                                                if(RESULT == null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                }
                                                            :}
                  | vector:v1 POR vector:v2                 {: RESULT = Matrices.producto(v1,v2); :}
                  | matriz:m CONCATENA escalar:n            {: RESULT = Matrices.concatena(m,n); :}
                  | matriz:m CONCATENA vector:v             {: 
                                                                if(Matrices.filas(m)==Matrices.dim(v)){
                                                                    RESULT = Matrices.concatena(m,v);
                                                                } else {
                                                                    error(Matrices.ERROR_FILAS_INSUF);
                                                                }
                                                            :}
                  | INVERSA AP matriz:m CP                  {: 
                                                                if(Matrices.columnas(m)==Matrices.filas(m)){
                                                                    RESULT = Matrices.inversa(m);
                                                                } else {
                                                                    error(Matrices.ERROR_INVERSA);
                                                                }
                                                            :}
                  | ADJUNTA AP matriz:m CP                  {: 
                                                                if(Matrices.columnas(m)==Matrices.filas(m)){
                                                                    RESULT = Matrices.adjunta(m);
                                                                } else {
                                                                    error(Matrices.ERROR_ADJUNTA);
                                                                }
                                                            :}
                  | TRANSPUESTA AP matriz:m CP              {: RESULT = Matrices.transpuesta(m); :}
                  | escalar:n POR matriz:m                  {: RESULT = Matrices.producto(n,m); :}
                  | matriz:m POR escalar:n                  {: RESULT = Matrices.producto(n,m); :}
                  | escalar:n MAS matriz:m                  {: RESULT = Matrices.suma(n,m); :}
                  | matriz:m MAS escalar:n                  {: RESULT = Matrices.suma(n,m); :}
                  | matriz:m1 MAS matriz:m2                 {:
                                                                if(Matrices.filas(m1)==Matrices.filas(m2)){
                                                                    if(Matrices.columnas(m1)==Matrices.columnas(m2)){
                                                                        RESULT = Matrices.suma(m1, m2);
                                                                    } else {
                                                                        error(Matrices.ERROR_SUMA);
                                                                    }
                                                                } else {
                                                                    error(Matrices.ERROR_SUMA);
                                                                }
                                                            :}
                  | matriz:m1 POR matriz:m2                 {: 
                                                                if(Matrices.columnas(m1)==Matrices.filas(m2)){
                                                                    RESULT = Matrices.producto(m1, m2);
                                                                } else {
                                                                    error(Matrices.ERROR_PROD);
                                                                }
                                                            :}
                  | matriz:m POR vector:v                   {: 
                                                                if(Matrices.columnas(m)==Matrices.dim(v)){
                                                                    RESULT = Matrices.producto(m, v);
                                                                } else {
                                                                    error(Matrices.ERROR_PROD);
                                                                }
                                                            :}
                  | vector:v POR matriz:m                   {: 
                                                                if(Matrices.filas(m)==Matrices.dim(v)){
                                                                    RESULT = Matrices.producto(v, m);
                                                                } else {
                                                                    error(Matrices.ERROR_PROD);
                                                                }
                                                            :}
                  | MATRIZ:m AP escalar:n1 COMA escalar:n2 CP  {: 
                                                                if(m==null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                } 
                                                                if(n1>Matrices.filas(m) || n1 <= 0){
                                                                    error(Matrices.ERROR_FILAS_INSUF);
                                                                }
                                                                if(n2>Matrices.columnas(m) || n2 <= 0){
                                                                    error(Matrices.ERROR_COLUM_INSUF);
                                                                }
                                                                RESULT = Matrices.subMatriz(m, n1.intValue(), n2.intValue());
                                                            :}
                  ;

vector          ::= ALL listaNumeros:ln CLL                 {: RESULT = Matrices.toVector(ln); :}
                  | VECTOR:s                                {: 
                                                                RESULT = s;
                                                                if(RESULT == null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                }
                                                            :}
                  | vector:v1 CONCATENA escalar:n           {: RESULT = Matrices.concatena(v1,n); :}
                  | escalar:n CONCATENA vector:v1           {: RESULT = Matrices.concatena(n,v1); :}
                  | vector:v1 CONCATENA vector:v2           {: RESULT = Matrices.concatena(v1,v2); :}
                  | vector:v1 CONCATENA conjunto:c1         {: RESULT = Matrices.concatena(v1, c1); :}
                  | conjunto:c1 CONCATENA vector:v1         {: RESULT = Matrices.concatena(c1, v1); :}
                  | vector:v1 MAS vector:v2                 {: 
                                                                if(Matrices.dim(v1)== Matrices.dim(v2)){
                                                                    RESULT = Matrices.suma(v1,v2);
                                                                } else {
                                                                    error(Matrices.ERROR_SUMA_VEC);
                                                                }
                                                                    
                                                            :}
                  | vector:v1 MAS escalar:n                 {: RESULT = Matrices.suma(n, v1); :}
                  | escalar:n MAS vector:v1                 {: RESULT = Matrices.suma(n, v1); :}
                  | vector:v1 POR escalar:n                 {: RESULT = Matrices.producto(n,v1); :}
                  | escalar:n POR vector:v1                 {: RESULT = Matrices.producto(n,v1); :}
                  ;

conjunto        ::= SET vector:v                            {: RESULT = Matrices.toSet(v); :}
                  | SET conjunto:c                          {: RESULT = Matrices.toSet(c); :}
                  | SET AP vector:v CP                      {: RESULT = Matrices.toSet(v); :}
                  | SET AP conjunto:c CP                    {: RESULT = Matrices.toSet(c); :}
                  | CONJUNTO:c                              {: 
                                                                RESULT = c;
                                                                if(RESULT == null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                }
                                                            :}
                  | conjunto:c1 CONCATENA conjunto:c2       {: 
                                                                double[] res = Matrices.concatena(c1, c2);
                                                                RESULT = Matrices.toSet(res);
                                                            :}
                  | conjunto:c1 INTERSECCION conjunto:c2    {: 
                                                                double[] res = Matrices.interseccion(c1, c2);
                                                                RESULT = Matrices.toSet(res);
                                                            :}
                  | conjunto:c1 INTERSECCION vector:v1      {: 
                                                                double[] v = Matrices.toSet(v1);
                                                                double[] res = Matrices.interseccion(c1, v);
                                                                RESULT = Matrices.toSet(res);
                                                            :}
                  | vector:v1 INTERSECCION conjunto:c1      {: 
                                                                double[] v = Matrices.toSet(v1);
                                                                double[] res = Matrices.interseccion(v, c1);
                                                                RESULT = Matrices.toSet(res);
                                                            :}
                  | vector:v1 INTERSECCION vector:v2        {: 
                                                                double[] v1p = Matrices.toSet(v1);
                                                                double[] v2p = Matrices.toSet(v2);
                                                                double[] res = Matrices.interseccion(v1p, v2p);
                                                                RESULT = Matrices.toSet(res);
                                                            :}
                  ;

pseudomatriz    ::= ALL pseudoListaFilas:lf CLL             {: RESULT = Matrices.toArray(lf); :}
                  ;

listaFilas      ::= listaFilas:lf COMA vector:v             {: 
                                                                if(lf.get(0).size()==Matrices.toArrayList(v).size()){
                                                                    RESULT = lf;
                                                                    RESULT.add(Matrices.toArrayList(v));
                                                                } else {
                                                                    error(Matrices.ERROR_FILAS);
                                                                }
                                                            :}
                  | vector:v                                {: 
                                                                RESULT = new ArrayList<ArrayList<Double>>();
                                                                RESULT.add(Matrices.toArrayList(v));
                                                            :}
                  ;

pseudoListaFilas::= pseudoListaFilas:lf COMA vector:v       {: 
                                                                RESULT = lf; 
                                                                RESULT.add(Matrices.toArrayList(v)); 
                                                            :}
                  | vector:v                                {: 
                                                                RESULT = new ArrayList<ArrayList<Double>>();
                                                                RESULT.add(Matrices.toArrayList(v));
                                                            :}
                  ;

listaNumeros    ::= listaNumeros:ln COMA escalar:n          {: RESULT = ln; RESULT.add(n); :}
                  | escalar:n                               {: 
                                                                RESULT = new ArrayList<Double>();
                                                                RESULT.add(n);
                                                            :}
                  |                                         {: RESULT = new ArrayList<Double>(); :}
                  ;

escalar           ::= MENOS NUMERO:n                          {: RESULT = -n; :} %prec MENOSUNARIO
                  | NUMERO:n                                  {: RESULT = n; :}
                  | escalar:v1 MAS escalar:v2                 {: RESULT = v1+v2; :}
                  | escalar:v1 MENOS escalar:v2               {: RESULT = v1-v2; :}
                  | escalar:v1 POR escalar:v2                 {: RESULT = v1*v2; :}
                  | escalar:v1 DIV escalar:v2                 {: RESULT = v1/v2; :}
                  ;
