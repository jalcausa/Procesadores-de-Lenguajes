import java_cup.runtime.*;
import java.util.ArrayList;

action code {: 
                public void error(String s){
                    System.out.println(s);
                    System.exit(-1);
                }
            :}

parser code {: 
                TablaSimbolos ts;
            :}

/* Terminals (tokens returned by the scanner). */

terminal            ALL, CLL, AP, CP, AC, CC;
terminal            COMA, PYC;
terminal            ASIG;
terminal            MAS, MENOS, POR, DIV, MENOSUNARIO;
terminal            PRINT, CONCATENA;
terminal            TRANSPUESTA, INVERSA, ADJUNTA;
terminal double[][] MATRIZ;
terminal double[]   VECTOR;
terminal String     IDENT;
terminal Double     NUMERO;


/* Non terminals */
non terminal                              axioma;
non terminal                              linea;
non terminal ArrayList<ArrayList<Double>> listaFilas;
non terminal ArrayList<Double>            listaNumeros;
non terminal double[][]                   matriz;
non terminal double[]                     vector;
non terminal Double                       valor;


/* Precedences */
precedence left     MAS, MENOS;
precedence left     POR, DIV;
precedence nonassoc MENOSUNARIO;


/* The grammar */

axioma          ::= axioma linea
                  | linea
                  ;

linea           ::= PRINT AP matriz:m CP PYC                {: Matrices.print(m); :}
                  | PRINT AP vector:v CP PYC                {: Matrices.print(v); :}
                  | IDENT:s ASIG matriz:m PYC               {: 
                                                                if(TablaSimbolos.buscar(s)==null){
                                                                    TablaSimbolos.insertar(s,m);
                                                                } else {
                                                                    error(TablaSimbolos.ERROR_DUPLICADA);
                                                                }
                                                            :}
                  | IDENT:s ASIG vector:v PYC               {: 
                                                                if(TablaSimbolos.buscarVector(s)==null){
                                                                    TablaSimbolos.insertarVector(s,v);
                                                                } else {
                                                                    error(TablaSimbolos.ERROR_DUPLICADA);
                                                                }
                                                            :}
                  ;

matriz          ::= ALL listaFilas:lf CLL                   {: RESULT = Matrices.toArray(lf); :}
                  | MATRIZ:s                                 {: 
                                                                RESULT = s;
                                                                if(RESULT == null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                }
                                                            :}
                  ;

vector          ::= ALL listaNumeros:ln CLL                 {: RESULT = Matrices.toVector(ln); :}
                  | VECTOR:s                                {: 
                                                                RESULT = s;
                                                                if(RESULT == null){
                                                                    error(TablaSimbolos.ERROR_NOEXISTE);
                                                                }
                                                            :}
                  ;

listaFilas      ::= listaFilas:lf COMA vector:v             {: 
                                                                if(lf.get(0).size()==Matrices.toArrayList(v).size()){
                                                                    RESULT = lf;
                                                                    RESULT.add(Matrices.toArrayList(v));
                                                                } else {
                                                                    error(Matrices.ERROR_FILAS);
                                                                }
                                                            :}
                  | vector:v                                {: 
                                                                RESULT = new ArrayList<ArrayList<Double>>();
                                                                RESULT.add(Matrices.toArrayList(v));
                                                            :}
                  ;

listaNumeros    ::= listaNumeros:ln COMA valor:n           {: RESULT = ln; RESULT.add(n); :}
                  | valor:n                                {: 
                                                                RESULT = new ArrayList<Double>();
                                                                RESULT.add(n);
                                                            :}
                  |                                         {: RESULT = new ArrayList<Double>(); :}
                  ;

valor           ::= MENOS NUMERO:n                          {: RESULT = -n; :} %prec MENOSUNARIO
                  | NUMERO:n                                {: RESULT = n; :}
                  | valor:v1 MAS valor:v2                   {: RESULT = v1+v2; :}
                  | valor:v1 MENOS valor:v2                 {: RESULT = v1-v2; :}
                  | valor:v1 POR valor:v2                   {: RESULT = v1*v2; :}
                  | valor:v1 DIV valor:v2                   {: RESULT = v1/v2; :}
                  ;
