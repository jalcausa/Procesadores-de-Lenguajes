import java_cup.runtime.*;
import java.util.*;

//Símbolos terminales
terminal MAS, MENOS, POR, DIV, MENOSUNARIO, IGUAL;
terminal AP, CP, ALL, CLL, PYC;
terminal MAYOR, MENOR, IGUALDAD, NOIGUALDAD, MENOREQ, MAYOREQ, AND, OR, NOT; 
terminal PRINT, IF, DO, WHILE, ELSE, FOR;
terminal String IDENT, ENTERO;


//Símbolos no terminales
non terminal axioma;
non terminal AST listaSentencias, sentencia;
non terminal EXPRESION expresion;
non terminal CONDICION condicion;

//Precedencias
precedence right IGUAL;
precedence left OR; 
precedence left AND;
precedence left MENOR, MAYOR, MENOREQ, MAYOREQ; 
precedence left MAS, MENOS;
precedence left POR, DIV;
precedence nonassoc NOT;
precedence nonassoc MENOSUNARIO;
precedence nonassoc IF;
precedence nonassoc ELSE;



//Gramática

axioma          ::= listaSentencias:ls                  {: ls.gc(); :}
                  ;

listaSentencias ::= sentencia:s                         {: RESULT = new AST(null, s); :}
                  | listaSentencias:ls sentencia:s      {: RESULT = new AST(ls, s); :}

                  ;

sentencia       ::= expresion:e PYC                     {: RESULT = e; :}
                  | PRINT AP expresion:e CP PYC         {: RESULT = new PRINT(e, null); :}
                  | ALL listaSentencias:ls CLL          {: RESULT = ls; :}
                  | IF AP condicion:c CP sentencia:s    {: RESULT = new IF(c, s); :}
                  | IF AP condicion:c CP sentencia:s1 ELSE sentencia:s2 {: IF p1 = new IF(c, s1); RESULT = new IFELSE(p1, s2); :}
                  ;

expresion       ::= expresion:e1 MAS expresion:e2       {: RESULT = new MAS(e1, e2); :}
                  | expresion:e1 MENOS expresion:e2     {: RESULT = new MENOS(e1, e2); :}
                  | expresion:e1 POR expresion:e2       {: RESULT = new POR(e1, e2); :}
                  | expresion:e1 DIV expresion:e2       {: RESULT = new DIV(e1, e2); :}
                  | MENOS expresion:e                   {: RESULT = new MENOS(null, e); :} %prec MENOSUNARIO
                  | AP expresion:e CP                   {: RESULT = e; :}
                  | IDENT:id IGUAL expresion:e          {: RESULT = new ASIG(id, e); :}
                  | IDENT:id                            {: RESULT = new CTE(id, null); :}
                  | ENTERO:n                            {: RESULT = new CTE(n, null); :}
                  ;

condicion       ::= expresion:e1 IGUALDAD expresion:e2  {: RESULT = new IGUALDAD(e1,e2); :}
                  | expresion:e1 NOIGUALDAD expresion:e {: RESULT = new NOIGUALDAD(e1,e); :}
                  | expresion:e1 MENOR expresion:e2     {: RESULT = new MENOR(e1,e2); :}
                  | expresion:e1 MENOREQ expresion:e    {: RESULT = new MENOREQ(e1,e); :}
                  | expresion:e1 MAYOR expresion:e2     {: RESULT = new MAYOR(e1,e2); :}
                  | expresion:e1 MAYOREQ expresion:e    {: RESULT = new MAYOREQ(e1,e); :}
                  | NOT condicion:c                     {: RESULT = new NOT(c,null); :}
                  | condicion:c1 AND condicion:c2       {: RESULT = new AND(c1, c2); :}
                  | condicion:c1 OR condicion:c2        {: RESULT = new OR(c1,c2); :}
                  | AP condicion:c CP                   {: RESULT = c; :}
                  ;