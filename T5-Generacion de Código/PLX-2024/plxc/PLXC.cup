import java_cup.runtime.*;
import java.util.*;


// SIMBOLOS TERMINALES

terminal MAS, MENOS, POR, DIV, MENOSUNARIO, IGUAL;
terminal AP, CP, ALL, CLL, AC, CC, COMA, PUNTO, PYC;
terminal MAYOR, MENOR, IGUALDAD, NOIGUALDAD, MENOREQ, MAYOREQ, AND, OR, NOT, IMPLICA; 
terminal PRINT, IF, DO, WHILE, ELSE, FOR, LENGTH;
terminal INT, FLOAT, CHAR, STRING, BOOLEAN;
terminal String IDENT, ENTERO, REAL, CADENA, CARACTER;
terminal String TRUE, FALSE;



// SIMBOLOS NO TERMINALES

non terminal axioma;
non terminal PRINT sentPrint;
non terminal IF sentIf;
non terminal IFELSE sentIfElse;
non terminal WHILE sentWhile;
non terminal DOWHILE sentDoWhile;
non terminal FOR sentFor;
non terminal AST bloque, listaSentencias, sentencia;
non terminal EXPRESION expresion;
non terminal DECLARVAR declarVariable;
non terminal DECLARVEC declarVector;
non terminal TIPO tipo;
non terminal VECTOR listaElems, expvector;

// PRECEDENCIAS 

precedence left COMA;
precedence right IGUAL;
precedence left IMPLICA;
precedence left OR; 
precedence left AND;
precedence left IGUALDAD, NOIGUALDAD, MENOR, MAYOR, MENOREQ, MAYOREQ; 
precedence left MAS, MENOS;
precedence left POR, DIV;
precedence left FLOAT, INT;
precedence left STRING, CHAR;
precedence right NOT;
precedence left MENOSUNARIO;
precedence nonassoc AP, CP, AC, CC;
precedence nonassoc IF;
precedence nonassoc ELSE;



// GRAMATICA

axioma          ::= listaSentencias:ls                  {: ls.gc(); :}
                  ;

listaSentencias ::= sentencia:s                         {: RESULT = new AST(null, s); :}
                  | listaSentencias:ls sentencia:s      {: RESULT = new AST(ls, s); :}

                  ;

sentencia       ::= expresion:e PYC                     {: RESULT = e; :}
                  | declarVariable:d PYC                {: RESULT = d; :}
                  | declarVector:d PYC                  {: RESULT = d; :}
                  | sentPrint:p                         {: RESULT = p; :}
                  | bloque:b                            {: RESULT = b; :}
                  | sentIf:i                            {: RESULT = i; :}
                  | sentIfElse:ie                       {: RESULT = ie; :}
                  | sentWhile:w                         {: RESULT = w; :}
                  | sentDoWhile:dw                      {: RESULT = dw; :}
                  | sentFor:f                           {: RESULT = f; :}
                  ;

sentPrint       ::= PRINT AP expresion:e CP PYC         {: RESULT = new PRINT(e, null); :}              // Resto de impresiones
                  | PRINT AP expvector:e CP PYC         {: RESULT = new PRINT(e, null); :}              // Impresion de vectores print({x1, x2, ..., xn});
                  ;

bloque          ::= ALL                                 {: TablaSimbolos.nuevaTabla(); :}               // Creamos una nueva tabla de simbolos para las variables locales del bloque
                    listaSentencias:ls 
                    CLL                                 {: 
                                                           TablaSimbolos.cierraTabla();                 // Cuando salimos del bloque cerramos tambien la tabla
                                                           RESULT = ls; 
                                                        :}
                  ;

sentIf          ::= IF AP expresion:c CP sentencia:s    {: RESULT = new IF(c, s); :}
                  ;

sentIfElse      ::= IF AP expresion:c CP sentencia:s1 ELSE sentencia:s2 
                                                        {: 
                                                            IF p1 = new IF(c, s1);                      //If auxiliar para un if-else
                                                            RESULT = new IFELSE(p1, s2); 
                                                        :} 
                  ;

sentWhile       ::= WHILE AP expresion:c CP sentencia:s {: RESULT = new WHILE(c,s); :}
                  ;

sentDoWhile     ::= DO sentencia:s WHILE AP expresion:c CP PYC 
                                                        {: 
                                                            RESULT = new DOWHILE(s, c); 
                                                        :}
                  ;

sentFor         ::= FOR AP expresion:e1 PYC expresion:c PYC expresion:e2 CP sentencia:s
                                                        {:                                              //Vemos el for como un while 
                                                            AST sentwhile = new AST(s, e2);             //Sentencias dentro del while (las del for y la actualizacion de la variable de control)
                                                            WHILE eqFor = new WHILE(c, sentwhile);      //While equivalente al bucle for
                                                            RESULT = new FOR(e1, eqFor);                //For formado por la declaracion de la variable de control y equivalencia en while
                                                        :}
                  ;


declarVariable  ::= tipo:t IDENT:id                     
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ha sido declarada previamente emitimos un error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            } else {                                    //En caso contrario la insertamos en la tabla de simbolos
                                                                TablaSimbolos.insertar(t, id);
                                                            }
                                                            RESULT = new DECLARVAR(t, id);
                                                        :}

                  | tipo:t IDENT:id IGUAL expresion:e   
                                                        {:  
                                                            ASIG dcha = new ASIG(id, e);                //Asignacion realizada
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ha sido declarada previamente emitimos un error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }
                                                            if (t.getTipo()!=e.getTipo()){              //Si los tipos no coinciden emitimos un error
                                                                Generador.error(Generador.E_TIPOS);
                                                            }
                                                            TablaSimbolos.insertar(t, id);              //En caso contrario insertamos en la tabla de simbolos
                                                            RESULT = new DECLARVAR(null, t, id, dcha);
                                                        :}

                  | declarVariable:d COMA IDENT:id IGUAL expresion:e
                                                        {: 
                                                            ASIG dcha = new ASIG(id, e);                //Asignacion realizada
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ha sido declarada previamente emitimos un error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }
                                                            if (d.getTipo().getTipo()!=e.getTipo()){    //Si los tipos no coinciden emitimos un error
                                                                Generador.error(Generador.E_TIPOS);
                                                            }
                                                            TablaSimbolos.insertar(d.getTipo(), id);    //En caso contrario insertamos en la tabla de simbolos
                                                            RESULT = new DECLARVAR(d, d.getTipo(), id, dcha);    
                                                        :}

                  | declarVariable:d COMA IDENT:id
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ha sido declarada emitimos un error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            } else {                                    //En caso contrario la insertamos en la tabla de simbolos
                                                                TablaSimbolos.insertar(d.getTipo(), id);
                                                            }
                                                            RESULT = new DECLARVAR(d, d.getTipo(), id, null);
                                                        :}
                  ;

declarVector    ::= tipo:t IDENT:id AC ENTERO:n CC
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Comprobamos si esta definida ya la variable 
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }
                                                            int tt = TIPO.VECTOR;                       //Establecemos tipo, subtipo y tamanyo del vector
                                                            int st = t.getTipo();
                                                            int tam = Integer.valueOf(n);
                                                            TIPO tipo = new TIPO(tt, st, tam);
                                                            TablaSimbolos.insertar(tipo, id);           //Lo insertamos en la tabla de simbolos
                                                            RESULT = new DECLARVEC(tipo, id);           //Guardamos la declaracion correspondiente
                                                        :}
                                                        
                  | tipo:t IDENT:id AC ENTERO:n CC IGUAL expvector:v
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ya esta declarada emitimos error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }
                                                            int tt = TIPO.VECTOR;
                                                            int st = t.getTipo();
                                                            int tam = Integer.valueOf(n);
                                                            if(st != v.getSubtipo()){                   //Comprobamos si hay error de subtipos
                                                                Generador.error(Generador.E_VTIPOS);
                                                            }
                                                            if(tam < v.getTam()){                       //Comprobamos si hay error de tamanyos
                                                                Generador.error(Generador.E_VLENGTH);
                                                            }                                           //En caso contrario insertamos en la tabla de simbolos
                                                            TIPO tipo = new TIPO(tt,st,tam);
                                                            TablaSimbolos.insertar(tipo, id);
                                                            ASIG dcha = new ASIG(id, v);
                                                            RESULT = new DECLARVEC(null, tipo, id, dcha);
                                                        :}

                  | declarVector:d COMA IDENT:id AC ENTERO:n CC
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ya esta declarada emitimos error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }                                           //En otro caso lo insertamos en la tabla de simbolos
                                                            int tt = TIPO.VECTOR;
                                                            int st = d.getTipo().getTipo();
                                                            int tam = Integer.valueOf(n);
                                                            TIPO tipo = new TIPO(tt, st, tam);
                                                            TablaSimbolos.insertar(tipo, id);
                                                            RESULT = new DECLARVEC(d, tipo, id, null);
                                                        :}

                  | declarVector:d COMA IDENT:id AC ENTERO:n CC IGUAL expvector:v
                                                        {: 
                                                            if(TablaSimbolos.estaDefinida(id)){         //Si la variable ya esta definida emitimos error
                                                                Generador.error(Generador.E_YADECLARADA);
                                                            }
                                                            int tt = TIPO.VECTOR;
                                                            int st = d.getTipo().getTipo();
                                                            int tam = Integer.valueOf(n);
                                                            if(st != v.getSubtipo()){                   //Si los subtipos no coinciden emitimos error
                                                                Generador.error(Generador.E_VTIPOS);
                                                            }
                                                            if(tam < v.getTam()){                       //Comprobamos errores de tamnyos
                                                                Generador.error(Generador.E_VLENGTH);
                                                            }
                                                            TIPO tipo = new TIPO(tt,st,tam);
                                                            TablaSimbolos.insertar(tipo, id);           //En caso contrario insertamos
                                                            ASIG dcha = new ASIG(id, v);
                                                            RESULT = new DECLARVEC(d, tipo, id, dcha);
                                                        :}
                  ;

tipo            ::= INT                                 {: RESULT = new TIPO(TIPO.ENTERO); :}
                  | FLOAT                               {: RESULT = new TIPO(TIPO.REAL); :}
                  | CHAR                                {: RESULT = new TIPO(TIPO.CARACTER); :}
                  | STRING                              {: RESULT = new TIPO(TIPO.CADENA); :}
                  | BOOLEAN                             {: RESULT = new TIPO(TIPO.CONDICION); :}
                  | tipo:t AC CC                        {: RESULT = new TIPO(TIPO.VECTOR, t.getTipo(), 0); :}
                  ;

expresion       ::= expresion:e1 IGUALDAD expresion:e2  {: RESULT = new IGUALDAD(e1,e2); :}
                  | expresion:e1 NOIGUALDAD expresion:e {: RESULT = new NOIGUALDAD(e1,e); :}
                  | expresion:e1 MENOR expresion:e2     {: RESULT = new MENOR(e1,e2); :}
                  | expresion:e1 MENOREQ expresion:e    {: RESULT = new MENOREQ(e1,e); :}
                  | expresion:e1 MAYOR expresion:e2     {: RESULT = new MAYOR(e1,e2); :}
                  | expresion:e1 MAYOREQ expresion:e    {: RESULT = new MAYOREQ(e1,e); :}
                  | NOT expresion:c                     {: RESULT = new NOT(c,null); :}
                  | expresion:c1 AND expresion:c2       {: RESULT = new AND(c1, c2); :}
                  | expresion:c1 OR expresion:c2        {: RESULT = new OR(c1,c2); :}
                  | expresion:c1 IMPLICA expresion:c2   {: RESULT = new IMPLICA(c1,c2); :}
                  | TRUE:t                              {: RESULT = new CTE(t, null, new TIPO(TIPO.CONDICION)); :}
                  | FALSE:f                             {: RESULT = new CTE(f, null, new TIPO(TIPO.CONDICION)); :}

                  | expresion:e1 MAS expresion:e2       {: RESULT = new MAS(e1, e2); :}
                  | expresion:e1 MENOS expresion:e2     {: RESULT = new MENOS(e1, e2); :}
                  | expresion:e1 POR expresion:e2       {: RESULT = new POR(e1, e2); :}
                  | expresion:e1 DIV expresion:e2       {: RESULT = new DIV(e1, e2); :}
                  | MENOS expresion:e                   {: RESULT = new MENOS(null, e); :} %prec MENOSUNARIO
                  | AP expresion:e CP                   {: RESULT = e; :}
                  | ENTERO:n                            {: RESULT = new CTE(n, null, new TIPO(TIPO.ENTERO)); :}
                  | REAL:r                              {: RESULT = new CTE(r, null, new TIPO(TIPO.REAL)); :}
                  | CARACTER:c                          {: RESULT = new CTE(c, null, new TIPO(TIPO.CARACTER)); :}
                  | CADENA:s                            {: RESULT = new CADENA(s, new TIPO(TIPO.CADENA)); :}
                  | AP tipo:t CP expresion:e            {: RESULT = new CASTING(t, e); :}
                  | IDENT:id                            {: 
                                                            TIPO tipo = TablaSimbolos.getTipo(id);
                                                            if(tipo==null){                                 //Si no ha sido aun declarada emitimos error
                                                                Generador.error(Generador.E_VND);
                                                            }
                                                            if(tipo.getTipo()==TIPO.CONDICION){             //Si es una condicion declarada devolvemos una condicion
                                                                RESULT = new CTECOND(id, tipo);
                                                            } else {
                                                                RESULT = new CTE(id, null, tipo); 
                                                            }
                                                        :}
                  | IDENT:id IGUAL expresion:e          {:  
                                                            TIPO tipo = TablaSimbolos.getTipo(id);
                                                            if(tipo == null){                           //Si no ha sido declarada emitimos error
                                                                Generador.error(Generador.E_VND);
                                                            }                                           //Si es una expresion se distinguen casos para asignar
                                                            RESULT = new ASIG(id, e);                   //Aqui se comprobara si los tipos son correctos
                                                        :}
                  | IDENT:id AC expresion:e CC          
                                                        {:  
                                                            if(!TablaSimbolos.estaDefinida(id)){        //Si no se ha declarado la variable se emite error
                                                                Generador.error(Generador.E_VND);
                                                            }
                                                            RESULT = new ELEMVECTOR(id, e, null);
                                                        :}
                  | IDENT:id AC expresion:e1 CC IGUAL expresion:e2 
                                                        {: 
                                                            if(!TablaSimbolos.estaDefinida(id)){        //Comprobamos si está definido el vector
                                                                Generador.error(Generador.E_VND);       //En caso negativo lanzamos un error
                                                            }
                                                            RESULT = new ELEMVECTOR(id, e1, e2);        //Guardamos el elemento del vector
                                                        :}
                  | IDENT:id IGUAL expvector:e          
                                                        {:
                                                            if(!TablaSimbolos.estaDefinida(id)){        //Si aun no ha sido declarado se imprime error
                                                                Generador.error(Generador.E_VND);
                                                            }
                                                            TIPO tipo = TablaSimbolos.getTipo(id);
                                                            if(tipo.getTipo()!=e.getTipo()){            //Si los tipos asinados no corresponden se imprime error
                                                                Generador.error();
                                                            }
                                                            RESULT = new ASIG(id, e);
                                                        :}
                  | IDENT:id PUNTO LENGTH               
                                                        {: 
                                                            if(!TablaSimbolos.estaDefinida(id)){        //Si no se ha definido emitimos error
                                                                Generador.error(Generador.E_VND);
                                                            }
                                                            TIPO tipo = new TIPO(TIPO.ENTERO);          //En caso de estar definida, guardamos la longitud como una constante mas
                                                            String newid = id+"_length";                //nombre de la longitud
                                                            RESULT = new CTE(newid, null, tipo);
                                                        :}
                  |                                     {: 
                                                           //Se añade para el bucle for con expresiones vacias
                                                           RESULT = new EXPRESION(null, null); 
                                                        :}
                  ;

expvector       ::= ALL listaElems:le CLL               {: RESULT = le; :}
                  ;

listaElems      ::= listaElems:le COMA expresion:e      
                                                        {: 
                                                            le.anyadir(e);
                                                            RESULT = le;
                                                        :}      
                  | expresion:e                         
                                                        {:
                                                            RESULT = new VECTOR();
                                                            RESULT.anyadir(e);
                                                        :}
                  ;
